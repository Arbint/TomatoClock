cmake_minimum_required(VERSION 3.26.3)

project(TomatoClock
VERSION 1.0.0
LANGUAGES C CXX
)

#important to set here other wise on a different computer, the default might be different.
set(CMAKE_CXX_STANDARD          11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS        OFF)

#MAC OSX ----------------
set(CMAKE_MACOSX_BUNDLE YES)
set(CMAKE_BUILD_TYPE "Release")

# Set the properties for the application bundle
set(MACOSX_BUNDLE_BUNDLE_VERSION "1.0.0")
set(MACOSX_BUNDLE_LONG_VERSION_STRING "1.0.0")
set(MACOSX_BUNDLE_SHORT_VERSION_STRING "1.0")
set(MACOSX_BUNDLE_BUNDLE_NAME "TomatoClock")
set(MACOSX_BUNDLE_GUI_IDENTIFIER "com.jingtian.TomatoClock")
#-----------

#Fetch External Libraries
include(FetchContent)

set(SFML_LIB_NAME SFML)
# SFML
FetchContent_Declare(
    ${SFML_LIB_NAME}
    GIT_REPOSITORY https://github.com/SFML/SFML
    GIT_TAG 2.5.1
    GIT_SHALLOW FALSE
)
FetchContent_MakeAvailable(${SFML_LIB_NAME})



#FUNCTIONS:
function(CopyLibToTarget LIB_NAME TARGET_NAME)    
    add_custom_command(TARGET ${TARGET_NAME}
    POST_BUILD
    COMMAND
    ${CMAKE_COMMAND} -E copy_if_different
    $<TARGET_FILE:${LIB_NAME}>
    $<TARGET_FILE_DIR:${TARGET_NAME}>
    )
endfunction()


#BEGIN TOMATO CONSCTURCT
Set(TOMATO_TARGET_NAME TomatoTargetName)

Set(TOMATO_TARGET_SRCS
    "${CMAKE_CURRENT_SOURCE_DIR}/src/main.cpp"

    "${CMAKE_CURRENT_SOURCE_DIR}/include/Application.h"
    "${CMAKE_CURRENT_SOURCE_DIR}/src/Application.cpp"
)

Set(TOMATO_TARGET_INCLUDE_DIR
    "${CMAKE_CURRENT_SOURCE_DIR}/include"
)

add_executable(${TOMATO_TARGET_NAME} ${TOMATO_TARGET_SRCS})

target_include_directories(
    ${TOMATO_TARGET_NAME} PUBLIC ${TOMATO_TARGET_INCLUDE_DIR}
)

target_link_libraries(${TOMATO_TARGET_NAME} PUBLIC 
    sfml-audio
    sfml-graphics
    sfml-system
    sfml-window
)

CopyLibToTarget(sfml-audio ${TOMATO_TARGET_NAME})
CopyLibToTarget(sfml-graphics ${TOMATO_TARGET_NAME})
CopyLibToTarget(sfml-system ${TOMATO_TARGET_NAME})
CopyLibToTarget(sfml-window ${TOMATO_TARGET_NAME})